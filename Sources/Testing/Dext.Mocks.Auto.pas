{***************************************************************************}
{                                                                           }
{           Dext Framework                                                  }
{                                                                           }
{           Copyright (C) 2025 Cesar Romero & Dext Contributors             }
{                                                                           }
{           Licensed under the Apache License, Version 2.0 (the "License"); }
{           you may not use this file except in compliance with the License.}
{           You may obtain a copy of the License at                         }
{                                                                           }
{               http://www.apache.org/licenses/LICENSE-2.0                  }
{                                                                           }
{           Unless required by applicable law or agreed to in writing,      }
{           software distributed under the License is distributed on an     }
{           "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,    }
{           either express or implied. See the License for the specific     }
{           language governing permissions and limitations under the        }
{           License.                                                        }
{                                                                           }
{***************************************************************************}

unit Dext.Mocks.Auto;

interface

uses
  System.SysUtils,
  System.Rtti,
  System.TypInfo,
  System.Generics.Collections,
  Dext.Interception,
  Dext.Interception.Proxy,
  Dext.Interception.ClassProxy,
  Dext.Mocks,
  Dext.Mocks.Interceptor;

type
  TAutoMocker = class
  private
    FInterceptors: TDictionary<PTypeInfo, IInterceptor>;
    FClassProxies: TObjectDictionary<PTypeInfo, TObject>; // OwnsProxies
    FMocks: TDictionary<PTypeInfo, IMock>;
    FContext: TRttiContext;
    function GetMockInterceptor(Info: PTypeInfo): IInterceptor;
  public
    constructor Create;
    destructor Destroy; override;
    function CreateInstance<T: class>: T;
    function GetMock<T>: Mock<T>;
  end;

implementation

{ TAutoMocker }

constructor TAutoMocker.Create;
begin
  FInterceptors := TDictionary<PTypeInfo, IInterceptor>.Create;
  FClassProxies := TObjectDictionary<PTypeInfo, TObject>.Create([doOwnsValues]);
  FMocks := TDictionary<PTypeInfo, IMock>.Create;
  FContext := TRttiContext.Create;
end;

destructor TAutoMocker.Destroy;
begin
  FMocks.Free;
  FClassProxies.Free;
  FInterceptors.Free;
  FContext.Free;
  inherited;
end;

function TAutoMocker.GetMockInterceptor(Info: PTypeInfo): IInterceptor;
begin
  if not FInterceptors.TryGetValue(Info, Result) then
  begin
    Result := TMockInterceptor.Create(TMockBehavior.Loose);
    FInterceptors.Add(Info, Result);
  end;
end;

function TAutoMocker.GetMock<T>: Mock<T>;
var
  Info: PTypeInfo;
  MockRef: IMock;
  Interceptor: IInterceptor;
  CProxy: TObject;
begin
  Info := TypeInfo(T);
  if not (Info.Kind in [tkInterface, tkClass]) then
    raise Exception.Create('T must be an interface or class');
    
  if FMocks.TryGetValue(Info, MockRef) then
    Exit(Mock<T>.FromInterface(MockRef as IMock<T>));

  Interceptor := GetMockInterceptor(Info);
  if Info.Kind = tkClass then
  begin
    if not FClassProxies.TryGetValue(Info, CProxy) then
    begin
      CProxy := TClassProxy.Create(Info.TypeData.ClassType, [Interceptor], False);
      FClassProxies.Add(Info, CProxy);
    end;
    Result := Mock<T>.FromInterface(TMock<T>.Create(TMockInterceptor(Interceptor), TClassProxy(CProxy), False));
  end
  else
    Result := Mock<T>.FromInterface(TMock<T>.Create(TMockInterceptor(Interceptor)));

  FMocks.Add(Info, Result.ProxyInterface);
end;

function TAutoMocker.CreateInstance<T>: T;
var
  RttiType: TRttiType;
  Method, ConstructorMethod: TRttiMethod;
  Params: TArray<TRttiParameter>;
  Args: TArray<TValue>;
  ParamType: TRttiType;
  ParamInfo: PTypeInfo;
  Interceptor: IInterceptor;
  CProxy: TObject;
  ProxyObj: TInterfaceProxy;
  ProxyIntf: IInterface;
  I: Integer;
begin
  RttiType := FContext.GetType(TypeInfo(T));
  if RttiType = nil then
    raise Exception.Create('Type not found in RTTI: ' + T.ClassName);

  ConstructorMethod := nil;
  for Method in RttiType.GetMethods do
  begin
    if Method.IsConstructor then
    begin
      if (ConstructorMethod = nil) or (Length(Method.GetParameters) > Length(ConstructorMethod.GetParameters)) then
        ConstructorMethod := Method;
    end;
  end;

  if ConstructorMethod = nil then
    raise Exception.Create('No constructor found for ' + T.ClassName);

  Params := ConstructorMethod.GetParameters;
  SetLength(Args, Length(Params));

  for I := 0 to High(Params) do
  begin
    ParamType := Params[I].ParamType;
    ParamInfo := ParamType.Handle;
    if (ParamType.TypeKind in [tkInterface, tkClass]) then
    begin
       Interceptor := GetMockInterceptor(ParamInfo);
       if ParamType.TypeKind = tkClass then
       begin
         if not FClassProxies.TryGetValue(ParamInfo, CProxy) then
         begin
           CProxy := TClassProxy.Create(ParamInfo.TypeData.ClassType, [Interceptor], False);
           FClassProxies.Add(ParamInfo, CProxy);
         end;
         Args[I] := TValue.From<TObject>(TClassProxy(CProxy).Instance);
       end
       else
       begin
         ProxyObj := TInterfaceProxy.Create(ParamInfo, [Interceptor], TValue.Empty);
         if ProxyObj.QueryInterface(ParamInfo.TypeData.Guid, ProxyIntf) <> S_OK then
           raise Exception.Create('Failed to query interface for mock: ' + ParamType.Name);
         TValue.Make(@ProxyIntf, ParamInfo, Args[I]);
       end;
    end
    else
    begin
       Args[I] := TValue.FromOrdinal(ParamType.Handle, 0); 
    end;
  end;

  Result := ConstructorMethod.Invoke(RttiType.AsInstance.MetaclassType, Args).AsType<T>;
end;

end.
